SET(LIBJPEG_MAJOR_VERSION "9")
SET(LIBJPEG_MINOR_VERSION_UPSTREAM "c")

# Convert minor version letter to number
IF ("${LIBJPEG_MINOR_VERSION_UPSTREAM}" MATCHES [aA])
  SET(LIBJPEG_MINOR_VERSION 0)
ELSEIF("${LIBJPEG_MINOR_VERSION_UPSTREAM}" MATCHES [bB])
  SET(LIBJPEG_MINOR_VERSION 1)
ELSEIF("${LIBJPEG_MINOR_VERSION_UPSTREAM}" MATCHES [cC])
  SET(LIBJPEG_MINOR_VERSION 2)
ELSEIF("${LIBJPEG_MINOR_VERSION_UPSTREAM}" MATCHES [dD])
  SET(LIBJPEG_MINOR_VERSION 3)
ELSEIF("${LIBJPEG_MINOR_VERSION_UPSTREAM}" MATCHES [eE])
  SET(LIBJPEG_MINOR_VERSION 4)
ELSEIF("${LIBJPEG_MINOR_VERSION_UPSTREAM}" MATCHES [fF])
  SET(LIBJPEG_MINOR_VERSION 5)
ELSEIF("${LIBJPEG_MINOR_VERSION_UPSTREAM}" MATCHES [gG])
  SET(LIBJPEG_MINOR_VERSION 6)
ELSEIF("${LIBJPEG_MINOR_VERSION_UPSTREAM}" MATCHES [hH])
  SET(LIBJPEG_MINOR_VERSION 7)
ELSE()
  MESSAGE(FATAL_ERROR "Minor LIBJPEG version exceeds version H")
ENDIF()

PROJECT(jpeg VERSION ${LIBJPEG_MAJOR_VERSION}.${LIBJPEG_MINOR_VERSION} LANGUAGES C)

# Try to download the source from the internet if no source folder is specified.
SET(LIBJPEG_FILE_VERSION ${LIBJPEG_MAJOR_VERSION}${LIBJPEG_MINOR_VERSION_UPSTREAM})
IF(NOT LIBJPEG_SOURCE_DIR)
  DownloadAndExtract("http://www.ijg.org/files/jpegsrc.v${LIBJPEG_FILE_VERSION}.tar.gz"
    ${CMAKE_CURRENT_SOURCE_DIR}/libjpeg-${LIBJPEG_FILE_VERSION}.tar.gz
    ${CMAKE_CURRENT_BINARY_DIR}
    )
  SET(_LIBJPEG_SOURCE_DIR ${CMAKE_CURRENT_BINARY_DIR}/jpeg-${LIBJPEG_FILE_VERSION})
ELSE()
  SET(_LIBJPEG_SOURCE_DIR ${LIBJPEG_SOURCE_DIR})

  # parse the full version number from jversion.h and include in LIBJPEG_FULL_VERSION
  FILE(READ ${_LIBJPEG_SOURCE_DIR}/jversion.h _jversion_h_contents)

  STRING(REGEX REPLACE ".*#define[ \t]+JVERSION[ \t]+\"([0-9]+[a-z]+).+\".*" "\\1" LIBJPEG_FULL_VERSION_DETECTED ${_jversion_h_contents})
  STRING(REGEX REPLACE "([0-9]+)[a-z]+" "\\1" LIBJPEG_MAJOR_VERSION_DETECTED ${LIBJPEG_FULL_VERSION_DETECTED})
  STRING(REGEX REPLACE "[0-9]+([a-z]+)" "\\1" LIBJPEG_MINOR_VERSION_DETECTED ${LIBJPEG_FULL_VERSION_DETECTED})

  SET(LIBJPEG_VERSION_DETECTED "${LIBJPEG_VERSION_MAJOR_DETECTED}${LIBJPEG_VERSION_MINOR_DETECTED}")
  IF(NOT ${LIBJPEG_VERSION_DETECTED} STREQUAL ${LIBJPEG_FILE_VERSION})
    MESSAGE(WARNING "LibJPEG version mismatch. Expecting '${LIBJPEG_FILE_VERSION}' but source code contains '${LIBJPEG_VERSION_DETECTED}'")
  ENDIF()
ENDIF()
SET(LIBJPEG_SOURCE_DIR ${_LIBJPEG_SOURCE_DIR} CACHE PATH "Path where to find LIBJPEG source")

# Use GNUInstallDirs to install libraries into correct
# locations on all platforms.
INCLUDE(GNUInstallDirs)

IF(MSVC)
  SET(jconfig "${LIBJPEG_SOURCE_DIR}/jconfig.vc")
ENDIF()

IF(MINGW)
  SET(jconfig "${LIBJPEG_SOURCE_DIR}/jconfig.vc")
ENDIF()

CONFIGURE_FILE(${jconfig} jconfig.h @ONLY)
CONFIGURE_FILE(${LIBJPEG_SOURCE_DIR}/jerror.h jerror.h COPYONLY)
CONFIGURE_FILE(${LIBJPEG_SOURCE_DIR}/jmorecfg.h jmorecfg.h COPYONLY)
CONFIGURE_FILE(${LIBJPEG_SOURCE_DIR}/jpeglib.h jpeglib.h COPYONLY)

SET(LIBJPEG_PUBLIC_HEADERS
  ${CMAKE_CURRENT_BINARY_DIR}/jconfig.h
  ${CMAKE_CURRENT_BINARY_DIR}/jerror.h
  ${CMAKE_CURRENT_BINARY_DIR}/jmorecfg.h
  ${CMAKE_CURRENT_BINARY_DIR}/jpeglib.h
)

SET(LIBJPEG_PRIVATE_HEADERS
  ${LIBJPEG_SOURCE_DIR}/jdct.h
  ${LIBJPEG_SOURCE_DIR}/jinclude.h
  ${LIBJPEG_SOURCE_DIR}/jmemsys.h
  ${LIBJPEG_SOURCE_DIR}/jpegint.h
  ${LIBJPEG_SOURCE_DIR}/jversion.h
)

SET(LIBJPEG_SRCS
  ${LIBJPEG_SOURCE_DIR}/jaricom.c
  ${LIBJPEG_SOURCE_DIR}/jcapimin.c
  ${LIBJPEG_SOURCE_DIR}/jcapistd.c
  ${LIBJPEG_SOURCE_DIR}/jcarith.c
  ${LIBJPEG_SOURCE_DIR}/jccoefct.c
  ${LIBJPEG_SOURCE_DIR}/jccolor.c
  ${LIBJPEG_SOURCE_DIR}/jcdctmgr.c
  ${LIBJPEG_SOURCE_DIR}/jchuff.c
  ${LIBJPEG_SOURCE_DIR}/jcinit.c
  ${LIBJPEG_SOURCE_DIR}/jcmainct.c
  ${LIBJPEG_SOURCE_DIR}/jcmarker.c
  ${LIBJPEG_SOURCE_DIR}/jcmaster.c
  ${LIBJPEG_SOURCE_DIR}/jcomapi.c
  ${LIBJPEG_SOURCE_DIR}/jcparam.c
  ${LIBJPEG_SOURCE_DIR}/jcprepct.c
  ${LIBJPEG_SOURCE_DIR}/jcsample.c
  ${LIBJPEG_SOURCE_DIR}/jctrans.c
  ${LIBJPEG_SOURCE_DIR}/jdapimin.c
  ${LIBJPEG_SOURCE_DIR}/jdapistd.c
  ${LIBJPEG_SOURCE_DIR}/jdarith.c
  ${LIBJPEG_SOURCE_DIR}/jdatadst.c
  ${LIBJPEG_SOURCE_DIR}/jdatasrc.c
  ${LIBJPEG_SOURCE_DIR}/jdcoefct.c
  ${LIBJPEG_SOURCE_DIR}/jdcolor.c
  ${LIBJPEG_SOURCE_DIR}/jddctmgr.c
  ${LIBJPEG_SOURCE_DIR}/jdhuff.c
  ${LIBJPEG_SOURCE_DIR}/jdinput.c
  ${LIBJPEG_SOURCE_DIR}/jdmainct.c
  ${LIBJPEG_SOURCE_DIR}/jdmarker.c
  ${LIBJPEG_SOURCE_DIR}/jdmaster.c
  ${LIBJPEG_SOURCE_DIR}/jdmerge.c
  ${LIBJPEG_SOURCE_DIR}/jdpostct.c
  ${LIBJPEG_SOURCE_DIR}/jdsample.c
  ${LIBJPEG_SOURCE_DIR}/jdtrans.c
  ${LIBJPEG_SOURCE_DIR}/jerror.c
  ${LIBJPEG_SOURCE_DIR}/jfdctflt.c
  ${LIBJPEG_SOURCE_DIR}/jfdctfst.c
  ${LIBJPEG_SOURCE_DIR}/jfdctint.c
  ${LIBJPEG_SOURCE_DIR}/jidctflt.c
  ${LIBJPEG_SOURCE_DIR}/jidctfst.c
  ${LIBJPEG_SOURCE_DIR}/jidctint.c
  ${LIBJPEG_SOURCE_DIR}/jmemmgr.c
  ${LIBJPEG_SOURCE_DIR}/jmemnobs.c
  ${LIBJPEG_SOURCE_DIR}/jquant1.c
  ${LIBJPEG_SOURCE_DIR}/jquant2.c
  ${LIBJPEG_SOURCE_DIR}/jutils.c
)

# Dependent include files
INCLUDE_DIRECTORIES(${LIBJPEG_SOURCE_DIR} ${CMAKE_CURRENT_BINARY_DIR})

IF(MSVC)
  LIST(APPEND PRIVATE_DEFINITIONS -D_CRT_SECURE_NO_WARNINGS) # Disable warnings for unsecure CRT functions
  LIST(APPEND PRIVATE_DEFINITIONS -D_LIB)
  LIST(APPEND PRIVATE_OPTIONS /Gy) # Eliminate Duplicate Strings
  SET(CMAKE_DEBUG_POSTFIX "d")
ENDIF(MSVC)

ADD_LIBRARY(jpeg STATIC ${LIBJPEG_SRCS} ${LIBJPEG_PRIVATE_HEADERS} ${LIBJPEG_PUBLIC_HEADERS})

# Dependent include files
TARGET_INCLUDE_DIRECTORIES(jpeg PUBLIC
  $<BUILD_INTERFACE:${CMAKE_CURRENT_BINARY_DIR}>
  $<INSTALL_INTERFACE:include>
  )

TARGET_COMPILE_DEFINITIONS(jpeg
  PRIVATE ${PRIVATE_DEFINITIONS}
  )

TARGET_COMPILE_OPTIONS(jpeg
  PRIVATE ${PRIVATE_OPTIONS}
  )

# INSTALL
INSTALL(TARGETS jpeg EXPORT jpeg_export
  RUNTIME DESTINATION ${CMAKE_INSTALL_BINDIR}
  LIBRARY DESTINATION ${CMAKE_INSTALL_LIBDIR}
  ARCHIVE DESTINATION ${CMAKE_INSTALL_LIBDIR}
  INCLUDES DESTINATION ${CMAKE_INSTALL_INCLUDEDIR}
  )

INSTALL(FILES
  ${LIBJPEG_PUBLIC_HEADERS}
  DESTINATION ${CMAKE_INSTALL_INCLUDEDIR}
  )

