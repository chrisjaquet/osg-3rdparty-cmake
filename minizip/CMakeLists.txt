SET(MINIZIP_VERSION "dac37702b3fab4068ac9a7c4a992df7f0e4f14df")

PROJECT(minizip LANGUAGES C)

# Check dependencies for MINIZIP
IF(NOT TARGET zlib)
  MESSAGE(FATAL_ERROR "Error: MINIZIP depends on ZLIB and ZLIB is not part of this build. Set USE_ZLIB=ON.")
ENDIF()

# Try to download the source from the internet if no source folder is specified.
IF(NOT MINIZIP_SOURCE_DIR)
  DownloadAndExtract("https://github.com/nmoinvaz/minizip/archive/${MINIZIP_VERSION}.zip"
    ${CMAKE_CURRENT_SOURCE_DIR}/minizip-${MINIZIP_VERSION}.zip
    ${CMAKE_CURRENT_BINARY_DIR}
    )
  SET(_MINIZIP_SOURCE_DIR ${CMAKE_CURRENT_BINARY_DIR}/minizip-${MINIZIP_VERSION})
ELSE()
  SET(_MINIZIP_SOURCE_DIR ${MINIZIP_SOURCE_DIR})
ENDIF()
SET(MINIZIP_SOURCE_DIR ${_MINIZIP_SOURCE_DIR} CACHE PATH "Path where to find MINIZIP source")

SET(MINIZIP_PUBLIC_HEADERS
  ${MINIZIP_SOURCE_DIR}/zip.h
  ${MINIZIP_SOURCE_DIR}/unzip.h
)

SET(MINIZIP_PRIVATE_HEADERS
  ${MINIZIP_SOURCE_DIR}/crypt.h
  ${MINIZIP_SOURCE_DIR}/ioapi.h
  ${MINIZIP_SOURCE_DIR}/ioapi_buf.h
  ${MINIZIP_SOURCE_DIR}/ioapi_mem.h
)

SET(MINIZIP_SRCS
  ${MINIZIP_SOURCE_DIR}/crypt.c
  ${MINIZIP_SOURCE_DIR}/ioapi.c
  ${MINIZIP_SOURCE_DIR}/ioapi_buf.c
  ${MINIZIP_SOURCE_DIR}/ioapi_mem.c
  ${MINIZIP_SOURCE_DIR}/unzip.c
  ${MINIZIP_SOURCE_DIR}/zip.c
)

IF(WIN32)
  list(APPEND MINIZIP_SRCS ${MINIZIP_SOURCE_DIR}/iowin32.c)
  list(APPEND MINIZIP_PRIVATE_HEADERS ${MINIZIP_SOURCE_DIR}/iowin32.h)
ENDIF()

IF(MSVC)
  LIST(APPEND PRIVATE_DEFINITIONS -D_CRT_SECURE_NO_WARNINGS) # Disable warnings for unsecure CRT functions

  SET(CMAKE_DEBUG_POSTFIX "d")
ENDIF(MSVC)

if(UNIX)
  LIST(APPEND PRIVATE_OPTIONS -fPIC)
  LIST(APPEND PRIVATE_OPTIONS -O3)
  LIST(APPEND PRIVATE_DEFINITIONS -D__USE_FILE_OFFSET64)
  LIST(APPEND PRIVATE_DEFINITIONS -D__USE_LARGEFILE64)
  LIST(APPEND PRIVATE_DEFINITIONS -D_LARGEFILE64_SOURCE)
  LIST(APPEND PRIVATE_DEFINITIONS -D_FILE_OFFSET_BIT=64)
endif()

ADD_LIBRARY(minizip STATIC ${MINIZIP_SRCS} ${MINIZIP_PUBLIC_HEADERS} ${MINIZIP_PRIVATE_HEADERS})

TARGET_INCLUDE_DIRECTORIES(minizip PUBLIC
  $<BUILD_INTERFACE:${CMAKE_CURRENT_BINARY_DIR}>
  $<BUILD_INTERFACE:${MINIZIP_SOURCE_DIR}/include>
  $<INSTALL_INTERFACE:include>
  )

TARGET_COMPILE_DEFINITIONS(minizip
  PRIVATE ${PRIVATE_DEFINITIONS}
  )

TARGET_COMPILE_OPTIONS(minizip
  PRIVATE ${PRIVATE_OPTIONS}
  )

TARGET_LINK_LIBRARIES(minizip zlib)

# INSTALL
INSTALL(TARGETS minizip EXPORT minizip_export
  RUNTIME DESTINATION ${CMAKE_INSTALL_BINDIR}
  LIBRARY DESTINATION ${CMAKE_INSTALL_LIBDIR}
  ARCHIVE DESTINATION ${CMAKE_INSTALL_LIBDIR}
  INCLUDES DESTINATION ${CMAKE_INSTALL_INCLUDEDIR}
  )

INSTALL(FILES
  ${MINIZIP_PUBLIC_HEADERS}
  DESTINATION ${CMAKE_INSTALL_INCLUDEDIR}
  )
